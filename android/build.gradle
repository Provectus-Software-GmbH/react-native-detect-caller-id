buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:8.1.0' // Updated to the latest version
    }
}

def getExtOrDefault(name, defaultValue) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : defaultValue
}

def isNewArchitectureEnabled() {
    // To opt-in for the New Architecture, you can either:
    // - Set `newArchEnabled` to true inside the `gradle.properties` file
    // - Invoke gradle with `-newArchEnabled=true`
    // - Set an environment variable `ORG_GRADLE_PROJECT_newArchEnabled=true`
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: 'com.android.library'

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

android {
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace "ru.nasvyazi.callerid"
        buildFeatures {
            buildConfig true
        }
    }

    compileSdkVersion getExtOrDefault('compileSdkVersion', 31)

    // Used to override the NDK path/version on internal CI or by allowing
    // users to customize the NDK path/version from their root project (e.g. for M1 support)
    if (rootProject.hasProperty("ndkPath")) {
        ndkPath rootProject.ext.ndkPath
    }
    if (rootProject.hasProperty("ndkVersion")) {
        ndkVersion rootProject.ext.ndkVersion
    }

    defaultConfig {
        minSdkVersion getExtOrDefault('minSdkVersion', 24)
        targetSdkVersion getExtOrDefault('targetSdkVersion', 31)
        versionCode 16
        versionName "1.8"

        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

        ndk {
            abiFilters (*reactNativeArchitectures())
        }
    }
    lintOptions {
        abortOnError false
    }
}

def reactNativeArchitectures() {
    def value = project.getProperties().get("reactNativeArchitectures")
    return value ? value.split(",") : [
        "armeabi-v7a",
        "x86",
        "x86_64",
        "arm64-v8a"
    ]
}


repositories {
    mavenCentral()
    google()
}

ext {
  supportLibraryVersion = '27.1.1'
  roomVersion = '1.1.0'
  lifeCycle = '1.1.1'
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "net.zetetic:android-database-sqlcipher:4.5.0"
    implementation "androidx.sqlite:sqlite:2.0.1"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"

    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    implementation "android.arch.lifecycle:extensions:$lifeCycle"
    annotationProcessor "android.arch.lifecycle:compiler:$lifeCycle"

    implementation 'com.facebook.react:react-native:+'

}
